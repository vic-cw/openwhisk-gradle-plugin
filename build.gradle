buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'com.cinnober.gradle', name: 'semver-git', version: '2.4.0'
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'eclipse'
}

apply plugin: 'com.cinnober.gradle.semver-git'

group = 'com.github.viccw'

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '27.1-jre'
    compile group: 'com.github.vic-cw', name: 'openwhisk-client-java', version:'50b41fd'
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = "$group.${rootProject.name}"
            implementationClass = 'com.github.viccw.openwhiskgradleplugin.OpenWhiskGradlePlugin'
        }
    }
}

plugins.withType(EclipsePlugin) {
    plugins.withType(JavaBasePlugin) {
        eclipse {
            classpath {
                file {
                    whenMerged { classpath ->
                        String gradleHome = gradle.getGradleHomeDir()
                            .absolutePath
                            .replace(File.separator, '/')
                        String gradleSourceDirectory = "${gradleHome}/src"
                        classpath.entries.each { entry ->
                            if (entry in org.gradle.plugins.ide.eclipse.model.AbstractLibrary
                                    && entry.library.path.contains('generated-gradle-jars')) {
                                entry.sourcePath =
                                    new org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory()
                                        .fromPath(gradleSourceDirectory)
                            }
                        }
                    }
                }
            }
        }
    }
}
